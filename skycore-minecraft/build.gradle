buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
        maven { url = 'https://maven.cleanroommc.com/' }
        mavenCentral()
    }
    dependencies {
        classpath "org.spongepowered:mixingradle:0.7.+"
    }
}

apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${projectVersion}"
group = mod_base_package

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    legacy {
        fixClasspath = true
        extractMappings = true
        attachMappings = true
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    maven { url = 'https://maven.cleanroommc.com/' }
    mavenCentral()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // skycore-core 核心库
    implementation project(':skycore-core')
    // Bedrock 粒子解析/运行库
    implementation project(':skycore-bedrockparticle')
    implementation 'org.joml:joml:1.10.5'

    // javax.vecmath 用于矩阵运算 (Chameleon 风格)
    implementation 'java3d:vecmath:1.3.1'

    // Mixin
    implementation ('zone.rong:mixinbooter:8.9') {
        transitive = false
    }
    annotationProcessor ('zone.rong:mixinbooter:8.9') {
        transitive = false
    }
    compileOnly 'org.spongepowered:mixin:0.8.5'
    annotationProcessor 'org.spongepowered:mixin:0.8.5'
    annotationProcessor 'org.ow2.asm:asm-tree:5.2'
    compileOnly 'org.ow2.asm:asm-tree:5.2'
}

mixin {
    add sourceSets.main, "${mod_id}.mixins.refmap.json"
}

jar {
    archiveBaseName = 'skycore-minecraft'
    archiveVersion = projectVersion
    from(project(':skycore-core').sourceSets.main.output)
    from(project(':skycore-bedrockparticle').sourceSets.main.output)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes([
            'Specification-Title': mod_id,
            'Specification-Vendor': mod_authors,
            'Specification-Version': '1',
            'FMLCorePluginContainsFMLMod': true,
            'FMLCorePlugin': 'org.mybad.minecraft.SkyCoreLoadingPlugin',
            'ForceLoadAsMod': true,
            'Implementation-Title': 'SkyCore-Minecraft',
            'Implementation-Version': projectVersion,
            'Implementation-Vendor': mod_authors,
            'Implementation-Timestamp': new Date().format('yyyy-MM-dd\'T\'HH:mm:ssZ')
        ])
    }
}

task sourceJar(type: Jar) {
    archiveBaseName = 'skycore-minecraft'
    archiveClassifier = 'sources'
    archiveVersion = projectVersion
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}
